apply plugin: 'com.android.application'
//apply tinker插件
//apply plugin: 'com.tencent.tinker.patch'
//greendao 插件
apply plugin: 'org.greenrobot.greendao'

//参考：https://segmentfault.com/a/1190000006940018

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    //recommend
    dexOptions {
        jumboMode = true
    }
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //AndroidManifest 中 TEST_CHANNEL_VALUE默认值
        manifestPlaceholders = [TEST_CHANNEL_VALUE: "official"]
        multiDexEnabled true

//        ndk { abiFilters "armeabi", "armeabi-v7a" }

        buildConfigField "String", "TINKER_ID", "\"${getTinkerIdValue()}\""//即 git sha
    }

    //签名配置，用于在下面的 buildTypes 中引用：signingConfig signingConfigs.release，
    // 也可直接在里面 buildTypes 里面配置
    signingConfigs {
        debug {
            // No debug config
//            storeFile file("${rootDir}/keystores/xqsg_debug.jks") //debug证书

            storeFile file("./keystore/debug.keystore")

        }

        release {
//            storeFile file("${rootDir}/keystores/xqsg.jks") //release证书
//            storePassword "test"                            //签名证书密码
//            keyAlias "test"                                 //别名
//            keyPassword "test"                              //别名密码
            try {
                storeFile file("./keystore/release.keystore")
                storePassword "testres"
                keyAlias "testres"
                keyPassword "testres"
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }
    }

    //用于生成不同编译类型的包，如debug和release包
    buildTypes {
        release {
//            storeFile file("../yourapp.keystore") //签名证书文件
//            storePassword "your password"         //签名证书密码
//            keyAlias "your alias"                 //别名
//            keyPassword "your password"           //别名密码
            buildConfigField "boolean", "SHOWLOG", "false"//在 BuildConfig 中会自动生成相应变量
            minifyEnabled true                               //开启混淆
            zipAlignEnabled true                             //开启Zipalign优化
            shrinkResources true                             //移除无用的resource文件，此项只有在开启混淆时才生效
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.release             //使用release证书签名

            applicationVariants.all { variant ->
                if (variant.buildType.name.equals('release')) {
                    variant.outputs.each { output ->
                        def outputFile = output.outputFile
                        if (outputFile != null && outputFile.name.endsWith(".apk")) {
                            def fileName = "${variant.productFlavors[0].name}_${defaultConfig.versionCode}.apk"
                            output.outputFile = new File(outputFile.parent, fileName)
                        }
                    }
                }
            }

        }
        debug {
            buildConfigField "boolean", "SHOWLOG", "true"       //配置日志开关
//            versionNameSuffix "-debug"                          //包名后缀为“-debug”
            zipAlignEnabled false                            //Zipalign优化
            shrinkResources false                            // 移除无用的resource文件
            minifyEnabled false                                 //是否混淆
//            signingConfig signingConfigs.debug               //使用debug证书签名

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                if (variant.buildType.name.equals('debug')) {
                    variant.outputs.each { output ->
                        def outputFile = output.outputFile
                        if (outputFile != null && outputFile.name.endsWith(".apk")) {
                            def fileName = "${variant.productFlavors[0].name}_${defaultConfig.versionCode}_debug.apk"
                            output.outputFile = new File(outputFile.parent + "/${variant.productFlavors[0].name}/", fileName)
                        }
                    }
                }
            }
        }
    }

    //配置 Lint 对 Java 和 XML 源文件的检查
    //参考：https://developer.android.com/studio/write/lint.html?hl=zh-cn
    lintOptions {
        // Turns off checks for the issue IDs you specify.
        disable 'TypographyFractions', 'TypographyQuotes'
        // Turns on checks for the issue IDs you specify. These checks are in
        // addition to the default lint checks.
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // To enable checks for only a subset of issue IDs and ignore all others,
        // list the issue IDs with the 'check' property instead. This property overrides
        // any issue IDs you enable or disable using the properties above.
        check 'NewApi', 'InlinedApi'
        // If set to true, turns off analysis progress reporting by lint.
        quiet true
        // if set to true (default), stops the build if errors are found.
        abortOnError false
        // if true, only report errors.
        ignoreWarnings true
    }

    //配置编译的jdk版本
    compileOptions {
        sourceCompatibility org.gradle.api.JavaVersion.VERSION_1_7
        targetCompatibility org.gradle.api.JavaVersion.VERSION_1_7
    }

    //动态地修改AndroidManifest中的渠道名,全部修改
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [ALL_CHANNEL_VALUE: name]

//        minSdkVersion rootProject.ext.minSdkVersion
//        targetSdkVersion rootProject.ext.targetSdkVersion
//        versionCode rootProject.ext.versionCode
//        versionName rootProject.ext.versionName
    }

    //    用于生成不同渠道的包
    productFlavors {
        binnan {
            applicationId "com.tincher.appcraft"

            buildConfigField "String", "GITSHA", "\"${gitSha()}\""  // git sha

//            minSdkVersion rootProject.ext.minSdkVersion
//            targetSdkVersion rootProject.ext.targetSdkVersion
//            versionCode rootProject.ext.versionCode
//            versionName rootProject.ext.versionName

            //AndroidManifest 中 TEST_CHANNEL_VALUE默认值
            manifestPlaceholders = [TEST_CHANNEL_VALUE: "binnan_test"]
            multiDexEnabled true
        }
        baidu {}
//        wandoujia {}
//        _360 {}        // 或“"360"{}”，数字需下划线开头或加上双引号

//          执行./gradlew assembleRelease ，将会打出所有渠道的release包；
//          执行./gradlew assembleWandoujia，将会打出豌豆荚渠道的release和debug版的包；
//          执行./gradlew assembleWandoujiaRelease将生成豌豆荚的release包。
//          因此，可以结合buildType和productFlavor生成不同的Build Variants，即类型与渠道不同的组合
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.Dkaishu:okhttp3:v1.0.0'
    //多渠道示例
    //    binnanCompile project(':devModule')
    //    baiduCompile 'com.example.go:example:v1.0'
    //Splash页以及轮播图
    compile 'cn.bingoogolapple:bga-banner:2.1.7@aar'
    //zxing 扫码
    //    compile 'com.google.zxing:core:3.3.0';
    //    compile 'com.google.zxing:android-core:3.3.0';
    //持久化
    compile 'org.greenrobot:greendao:3.2.2'
    //上拉刷新下拉加载
    compile 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.3'
    //二维码
    compile 'com.github.Dkaishu:ZXingLib:v1.0.1'
    //ArcGIS
    compile 'com.esri.arcgisruntime:arcgis-android:100.1.0'
    compile files('libs/google_binded_classes-dex2jar.jar')

    //tinker
    //需要multidex
    compile "com.android.support:multidex:1.0.1"
    //optional, help to generate the final application
    provided('com.tencent.tinker:tinker-android-anno:1.9.2')
    //tinker's main Android lib
    compile('com.tencent.tinker:tinker-android-lib:1.9.2')
}

greendao {
    //This is used by the *OpenHelpers classes to migrate between schema versions.
    // If you change your entity/database schema,
    // this value has to be increased. Defaults to 1.
    schemaVersion 2
}

/**
 * *********************以下是 tinker 的脚本配置*******************************************************
 * https://github.com/Tencent/tinker/wiki/Tinker-%E6%8E%A5%E5%85%A5%E6%8C%87%E5%8D%97
 *
 * 使用指南：
 *
 *  要打指定版本与当前版本的patch需要：
 *  1.找到指定版本的 tinkerOldApkPath，tinkerApplyMappingPath，tinkerApplyResourcePath，并在下方配置。若未混淆则不用
 *  配置tinkerApplyMappingPath,若是使用 Flavors 多渠道打包，仅需要配置tinkerBuildFlavorDirectory一相即可；
 *  2.Terminal 执行 相应的patch命令 ，如 gradlew tinkerPatchBinnanDebug ，等待编译完成即可在build/outputs/thinkerpatch 目录下找到相应文件。
 *
 * Android端加载patch：
 * 1.将patch文件下载到本地目录, patch .apk 文件路径记为 patchLocation
 * 2.恰当时机调用TinkerInstaller.onReceiveUpgradePatch(context，patchLocation)，重启应用即可完成热修复，成功率95%以上。
 *
 *
 */
def bakPath = file("${rootDir}/hotfix/")
def buildPath = file("${rootDir}/outputs")

/**
 * you can use assembleRelease to build you base apk
 * use tinkerPatchRelease -POLD_APK=  -PAPPLY_MAPPING=  -PAPPLY_RESOURCE= to build patch
 * add apk from the build/bakApk
 *
 * gradlew tinkerPatchBinnanDebug
 */
ext {
    //for some reason, you may want to ignore tinkerBuild, such as instant run debug build?
    tinkerEnabled = true

    //for normal build
    //old apk file to build patch apk  app-0129-15-38-32\binnan
    tinkerOldApkPath = "${bakPath}/app-0205-16-42-28/binnan/binnan_1_debug.apk"
    //proguard mapping file to build patch apk
    tinkerApplyMappingPath = "${bakPath}/app-debug-1018-17-32-47-mapping.txt"
    //resource R.txt to build patch apk, must input if there is resource changed
    tinkerApplyResourcePath = "${bakPath}/binnan/app-binnan-debug-R.txt"

    //only use for build all flavor, if not, just ignore this field //多flavor 仅仅需要配置此项
    tinkerBuildFlavorDirectory = "${bakPath}/app-0207-08-23-26"
//    D:\myTest\oschina-android-app-v2.8.8\AppCraft\app\build\bakApk\app-0129-15-38-32
}

def gitSha() {
    try {
        String gitRev = 'git rev-parse --short HEAD'.execute(null, project.rootDir).text.trim()
        if (gitRev == null) {
            throw new GradleException("can't get git rev, you should add git to system path or just input test value, such as 'testTinkerId'")
        }
        return gitRev
    } catch (Exception e) {
        throw new GradleException("can't get git rev, you should add git to system path or just input test value, such as 'testTinkerId'")
    }
}


def getOldApkPath() {
    return hasProperty("OLD_APK") ? OLD_APK : ext.tinkerOldApkPath
}

def getApplyMappingPath() {
    return hasProperty("APPLY_MAPPING") ? APPLY_MAPPING : ext.tinkerApplyMappingPath
}

def getApplyResourceMappingPath() {
    return hasProperty("APPLY_RESOURCE") ? APPLY_RESOURCE : ext.tinkerApplyResourcePath
}

def getTinkerIdValue() {
    return hasProperty("TINKER_ID") ? TINKER_ID : gitSha()
}

def buildWithTinker() {
    return hasProperty("TINKER_ENABLE") ? TINKER_ENABLE : ext.tinkerEnabled
}

def getTinkerBuildFlavorDirectory() {
    return ext.tinkerBuildFlavorDirectory
}

if (buildWithTinker()) {
    apply plugin: 'com.tencent.tinker.patch'

    tinkerPatch {
        /**
         * necessary，default 'null'
         * the old apk path, use to diff with the new apk to build
         * add apk from the build/bakApk
         */
        oldApk = getOldApkPath()
        /**
         * optional，default 'false'
         * there are some cases we may get some warnings
         * if ignoreWarning is true, we would just assert the patch process
         * case 1: minSdkVersion is below 14, but you are using dexMode with raw.
         *         it must be crash when load.
         * case 2: newly added Android Component in AndroidManifest.xml,
         *         it must be crash when load.
         * case 3: loader classes in dex.loader{} are not keep in the main dex,
         *         it must be let tinker not work.
         * case 4: loader classes in dex.loader{} changes,
         *         loader classes is ues to load patch dex. it is useless to change them.
         *         it won't crash, but these changes can't effect. you may ignore it
         * case 5: resources.arsc has changed, but we don't use applyResourceMapping to build
         */
        ignoreWarning = true

        /**
         * optional，default 'true'
         * whether sign the patch file
         * if not, you must do yourself. otherwise it can't check success during the patch loading
         * we will use the sign config with your build type
         */
        useSign = true

        /**
         * optional，default 'true'
         * whether use tinker to build
         */
        tinkerEnable = buildWithTinker()

        /**
         * Warning, applyMapping will affect the normal android build!
         */
        buildConfig {
            /**
             * optional，default 'null'
             * if we use tinkerPatch to build the patch apk, you'd better to apply the old
             * apk mapping file if minifyEnabled is enable!
             * Warning:
             * you must be careful that it will affect the normal assemble build!
             */
            applyMapping = getApplyMappingPath()
            /**
             * optional，default 'null'
             * It is nice to keep the resource id from R.txt file to reduce java changes
             */
            applyResourceMapping = getApplyResourceMappingPath()

            /**
             * necessary，default 'null'
             * because we don't want to check the base apk with md5 in the runtime(it is slow)
             * tinkerId is use to identify the unique base apk when the patch is tried to apply.
             * we can use git rev, svn rev or simply versionCode.
             * we will gen the tinkerId in your manifest automatic
             */
            tinkerId = getTinkerIdValue()

            /**
             * if keepDexApply is true, class in which dex refer to the old apk.
             * open this can reduce the dex diff file size.
             */
            keepDexApply = false

            /**
             * optional, default 'false'
             * Whether tinker should treat the base apk as the one being protected by app
             * protection tools.
             * If this attribute is true, the generated patch package will contain a
             * dex including all changed classes instead of any dexdiff patch-info files.
             */
            isProtectedApp = false
        }

        dex {
            /**
             * optional，default 'jar'
             * only can be 'raw' or 'jar'. for raw, we would keep its original format
             * for jar, we would repack dexes with zip format.
             * if you want to support below 14, you must use jar
             * or you want to save rom or check quicker, you can use raw mode also
             */
            dexMode = "jar"

            /**
             * necessary，default '[]'
             * what dexes in apk are expected to deal with tinkerPatch
             * it support * or ? pattern.
             */
            pattern = ["classes*.dex",
                       "assets/secondary-dex-?.jar"]
            /**
             * necessary，default '[]'
             * Warning, it is very very important, loader classes can't change with patch.
             * thus, they will be removed from patch dexes.
             * you must put the following class into main dex.
             * Simply, you should add your own application {@code tinker.sample.android.SampleApplication}
             * own tinkerLoader, and the classes you use in them
             *
             */
            loader = [
                    //use sample, let BaseBuildInfo unchangeable with tinker
//                    "com.tincher.appcraft.base.BaseBuildInfo"
                    "com.tincher.appcraft.base.BaseApplication"
            ]
        }

        lib {
            /**
             * optional，default '[]'
             * what library in apk are expected to deal with tinkerPatch
             * it support * or ? pattern.
             * for library in assets, we would just recover them in the patch directory
             * you can get them in TinkerLoadResult with Tinker
             */
            pattern = ["lib/*/*.so"]
        }

        res {
            /**
             * optional，default '[]'
             * what resource in apk are expected to deal with tinkerPatch
             * it support * or ? pattern.
             * you must include all your resources in apk here,
             * otherwise, they won't repack in the new apk resources.
             */
            pattern = ["res/*", "assets/*", "resources.arsc", "AndroidManifest.xml"]

            /**
             * optional，default '[]'
             * the resource file exclude patterns, ignore add, delete or modify resource change
             * it support * or ? pattern.
             * Warning, we can only use for files no relative with resources.arsc
             */
            ignoreChange = ["assets/sample_meta.txt"]

            /**
             * default 100kb
             * for modify resource, if it is larger than 'largeModSize'
             * we would like to use bsdiff algorithm to reduce patch file size
             */
            largeModSize = 100
        }

        packageConfig {
            /**
             * optional，default 'TINKER_ID, TINKER_ID_VALUE' 'NEW_TINKER_ID, NEW_TINKER_ID_VALUE'
             * package meta file gen. path is assets/package_meta.txt in patch file
             * you can use securityCheck.getPackageProperties() in your ownPackageCheck method
             * or TinkerLoadResult.getPackageConfigByName
             * we will get the TINKER_ID from the old apk manifest for you automatic,
             * other config files (such as patchMessage below)is not necessary
             */
            configField("patchMessage", "tinker is sample to use")
            /**
             * just a sample case, you can use such as sdkVersion, brand, channel...
             * you can parse it in the SamplePatchListener.
             * Then you can use patch conditional!
             */
            configField("platform", "all")
            /**
             * patch version via packageConfig
             */
            configField("patchVersion", "1.0")
        }
        //or you can add config filed outside, or get meta value from old apk
        //project.tinkerPatch.packageConfig.configField("test1", project.tinkerPatch.packageConfig.getMetaDataFromOldApk("Test"))
        //project.tinkerPatch.packageConfig.configField("test2", "sample")

        /**
         * if you don't use zipArtifact or path, we just use 7za to try
         */
        sevenZip {
            /**
             * optional，default '7za'
             * the 7zip artifact path, it will use the right 7za with your platform
             */
            zipArtifact = "com.tencent.mm:SevenZip:1.1.10"
            /**
             * optional，default '7za'
             * you can specify the 7za path yourself, it will overwrite the zipArtifact value
             */
//        path = "/usr/local/bin/7za"
        }
    }

    List<String> flavors = new ArrayList<>();
    project.android.productFlavors.each { flavor ->
        flavors.add(flavor.name)
    }
    boolean hasFlavors = flavors.size() > 0
    def date = new Date().format("MMdd-HH-mm-ss")

    /**
     * bak apk and mapping
     */
    android.applicationVariants.all { variant ->
        /**
         * task type, you want to bak
         */
        def taskName = variant.name

        tasks.all {
            if ("assemble${taskName.capitalize()}".equalsIgnoreCase(it.name)) {

                it.doLast {
                    copy {
                        def fileNamePrefix = "${project.name}-${variant.baseName}"
                        def newFileNamePrefix = hasFlavors ? "${fileNamePrefix}" : "${fileNamePrefix}-${date}"

                        def destPath = hasFlavors ? file("${bakPath}/${project.name}-${date}/${variant.flavorName}") : bakPath
                        from variant.outputs.outputFile
                        into destPath
                        rename { String fileName ->
                            fileName.replace("${fileNamePrefix}.apk", "${newFileNamePrefix}.apk")
                        }

                        from "${buildDir}/outputs/mapping/${variant.dirName}/mapping.txt"
                        into destPath
                        rename { String fileName ->
                            fileName.replace("mapping.txt", "${newFileNamePrefix}-mapping.txt")
                        }

                        from "${buildDir}/intermediates/symbols/${variant.dirName}/R.txt"
                        into destPath
                        rename { String fileName ->
                            fileName.replace("R.txt", "${newFileNamePrefix}-R.txt")
                        }
                    }
                }
            }
        }
    }
    project.afterEvaluate {
        //sample use for build all flavor for one time
        if (hasFlavors) {
            task(tinkerPatchAllFlavorRelease) {
                group = 'tinker'
                def originOldPath = getTinkerBuildFlavorDirectory()
                for (String flavor : flavors) {
                    def tinkerTask = tasks.getByName("tinkerPatch${flavor.capitalize()}Release")
                    dependsOn tinkerTask
                    def preAssembleTask = tasks.getByName("process${flavor.capitalize()}ReleaseManifest")
                    preAssembleTask.doFirst {
                        String flavorName = preAssembleTask.name.substring(7, 8).toLowerCase() + preAssembleTask.name.substring(8, preAssembleTask.name.length() - 15)
                        project.tinkerPatch.oldApk = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-release.apk"
                        project.tinkerPatch.buildConfig.applyMapping = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-release-mapping.txt"
                        project.tinkerPatch.buildConfig.applyResourceMapping = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-release-R.txt"

                    }

                }
            }

            task(tinkerPatchAllFlavorDebug) {
                group = 'tinker'
                def originOldPath = getTinkerBuildFlavorDirectory()
                for (String flavor : flavors) {
                    def tinkerTask = tasks.getByName("tinkerPatch${flavor.capitalize()}Debug")
                    dependsOn tinkerTask
                    def preAssembleTask = tasks.getByName("process${flavor.capitalize()}DebugManifest")
                    preAssembleTask.doFirst {
                        String flavorName = preAssembleTask.name.substring(7, 8).toLowerCase() + preAssembleTask.name.substring(8, preAssembleTask.name.length() - 13)
//                        def fileName = "${variant.productFlavors[0].name}_${defaultConfig.versionCode}_debug.apk"
//                        output.outputFile = new File(outputFile.parent + "/${variant.productFlavors[0].name}/", fileName)
//                        project.tinkerPatch.oldApk = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-debug.apk"
                        project.tinkerPatch.oldApk = "${originOldPath}/${flavorName}/${flavorName}_${rootProject.ext.versionCode}_debug.apk"
                        project.tinkerPatch.buildConfig.applyMapping = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-debug-mapping.txt"
                        project.tinkerPatch.buildConfig.applyResourceMapping = "${originOldPath}/${flavorName}/${project.name}-${flavorName}-debug-R.txt"
                    }
                }
            }
        }
    }
    /**
     ***********************以上是 tinker 的脚本配置*******************************************************
     */
}
